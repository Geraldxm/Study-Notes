## 1. 可编程元件

## 键盘

X1~X6作为数码管选中信号，同时X1~X4作为键盘扫描信号。对响应的列输出0来扫描。

Y1~Y4是扫描得到的信号。有按钮按下的行是0。

```ASM
DATA SEGMENT
FLAG    DB 00H
DATA ENDS

SCAN:
    MOV AL,11110111B    ;需要扫描的列，输出0
    MOV CX,04H          ;一共需要扫描4列
M1:
    MOV DX,A8255        ;选择A口
    OUT DX,AL           ;从X口输出列选信号
    SHR AL,1            ;0右移一位
    OR AL,11110000B
    PUSH AX         ;保存列选信号
    PUSH CX         ;保存循环计数
    MOV DX,C8255        ;从C口读入
    IN AL,DX            ;从 C-Y 输入键盘反馈信号
    AND AL,0FH          ;Y口接8255的C端口低四位，去除多余信息
    CMP AL,0FH          ;如果低四位是1111，表明没有按键按下
    JE M2
    CALL JUDGE       ;没有跳去M2，则表明有按键按下
    JMP M3      ;如果有按键按下，则跳过M2（互斥计数）阶段
M2:
    CMP FLAG,00H    ;维持互斥信号，FLAG=MAX(0,FLAG-1)
    JE M3           ;FLAG=0表示4列都没有按键按下，FLAG初值为4
    ; 如果FLAG=0,说明对4列的一次扫描完毕
    ; 否则就自减FLAG
    DEC FLAG        ;每扫描一列，并且没有按键按下时，FLAG自减1次
M3:
    CALL CLEAR      ;显示数码管的内容
    POP CX      ;先取出循环计数
    POP AX      ;再取出列选信号
    CALL DELAY  ;稍微延迟
    LOOP M1
    JMP SCAN
JUDGE:
    NOT AL          ;NOT取反（从这里开始，通过Y口输入信息来判断按下的按键）
    AND AX,0FH      ;AND取低4位（详情见下边第4点“关于按键判断”）
    CMP AL,01H  ;0001
        JE D1
    CMP AL,02H  ;0010
        JE D2
    CMP AL,04H  ;0100
        JE D3
    CMP AL,08H  ;1000
        JE D4
D1: MOV AL,00H ;0000 0100
    JMP D5
D2: MOV AL,04H ;0000 1000
    JMP D5
D3: MOV AL,08H ;0000 1100
    JMP D5
D4: MOV AL,0CH ;0001 0000
    JMP D5
D5: ADD AL,CL
    SUB AL,01H
    MOV SI,AX           ;到这里，按下的按键被转化为索引值
    CMP FLAG,00H        ;是否存在互斥，非0表示存在互斥
    JNE JUDGE_END

	CMP SI,0AH         ;A ~ F按键的if-else操作
        JNE D0A        ;不等于A则跳转到下一个，否则调用PRESS_A
        CALL PRESS_A
        JMP JUDGE_END
    D0A:
    CMP SI,0BH
        JNE D0B
        CALL PRESS_B
        JMP JUDGE_END
    D0B:
    CMP SI,0CH
        JNE D0C
        CALL PRESS_C
        JMP JUDGE_END
    D0C:
    CMP SI,0DH
        JNE D0D
        CALL PRESS_D
        JMP JUDGE_END
    D0D:
    CMP SI,0EH
        JNE D0E
        CALL PRESS_E
        JMP JUDGE_END
    D0E:
    CMP SI,0FH
        JNE D0F
        CALL PRESS_F
        JMP JUDGE_END
    D0F:


JUDGE_END:
    MOV FLAG,04H        ;互斥
    RET
```

## 开关

```ASM
C8255 EQU 0644H
MOV DX,C8255       ;从C口读入
IN AL,DX            ;从 C-Y 输入键盘反馈信号
```

## 数码管

数码管要显示，需要先用X1~X6输出位选码，注意看数码管电路对应关系。

（低位接低位，则最右侧数码管为11011111。）

然后再在abcdefg输出段选码。注意延迟，否则看不清！

```ASM
DATA SEGMENT
TAB:
    DB 3FH,06H,5BH,4FH,66H,6DH,7DH,07H,7FH,6FH,77H,7CH,39H,5EH,79H,71H
DATA ENDS
CODE SEGMENT
	ASSUME CS:CODE,DS:DATA
START:
	MOV AX,DATA
	MOV DS,AX
	...
;使用TAB，先存放首地址，然后取偏移，下面用 MOV AL,[BX+SI] 来取用
    LEA BX,TAB      ;存放TAB的首地址
    MOV SI,00H      ;地址偏移量

```

## 8254 定时计数器

先初始化。初始字中选择计数器和工作方式，然后在计数器中写入计数值。

```ASM
    A8254 EQU 0600H
    B8254 EQU 0602H
    C8254 EQU 0604H
    CON8254 EQU 0606H

	MOV DX, CON8254
	MOV AL, <控制字>
	OUT DX, AL
	MOV DX, B8254
	MOV AL, <计数值>
	OUT DX, AL
	CALL lldelay
```

周期一秒的方波

```ASM
SET8254:
    MOV AL,00110110B            ;计时器 0 以方式 3 工作
    MOV DX,M8254
    OUT DX,AL
    MOV DX,A8254            ;初值设置4800H = 18432D；若要实现一秒一中断，需要接入时钟源为18.432Khz
    MOV AL,00H
    OUT DX,AL
    MOV AL,48H
    OUT DX,AL
```

## D/A 转换

不用初始化，输出完之后要根据自己想要的效果，控制一下延迟。

```ASM
mov dx, port_da
mov al, <num>
out dx, al
call delay
```

## A/D 转换

如果要使用示波器观察波形，连接 A/D 转换单元。连接 IN0，地址三根线接 GND（000选中IN0）。IOY3 接 A/D单元的 CS。CLK 接 CLK。也要接WR/RD，数据接系统总线的DATA。

```ASM
port_ad equ 0640h

MOV DX, PORT_AD
MOV AL, 0ABH  ;随便发送一个信号开始转换
        out dx, al
        call DELAY  ;等待转换结果,必须延迟
        IN AL, DX   ; ad results

```

## 中断 8259

```ASM
INIT_8259:
        ;优先级数字越小越高，中断6的优先级高于中断7
        MOV AX, OFFSET MIR7     ;取MIR7的中断入口地址
        MOV SI, 003CH           ;中断矢量地址，和每个中断入口对应
        MOV [SI], AX            ;填IRQ7的偏移
        MOV AX, CS              ;段地址
        MOV SI, 003EH
        MOV [SI], AX            ;填IRQ7的段地址
        MOV AX, OFFSET MIR6
        MOV SI, 0038H
        MOV [SI], AX
        MOV AX, CS
        MOV SI, 003AH
        MOV [SI], AX
        CLI;关中断
        
        ;初始化主片8259
        MOV AL, 11H
        OUT 20H, AL             ;ICW1, 标志位,需要icw4
        MOV AL, 08H             ;ICW2, 00001***
        OUT 21H, AL             
        ;这里ICW2取00001***的原因应该是pdf-49页中,主片中断功能调用为
        ;08H~0FH,也就是00001***
        MOV AL, 04H
        OUT 21H, AL             ;ICW3
        MOV AL, 01H
        OUT 21H, AL             ;ICW4
        MOV AL, 2FH
        ;操作命令子OCW，控制是否屏蔽信号
        OUT 21H, AL             ;主8259 OCW1, 00101111
        STI

MIR6:   STI
        ;CALL DELAY

        MOV AX, 0136H           ;显示字符"6 "
        INT 10H                 
        MOV AX, 0120H
        INT 10H
        
        ;CALL DELAY
        ;CALL DELAY
        MOV AL, 20H
        OUT 20H, AL             ;中断结束命令
        IRET

MIR7:   STI
        ;CALL DELAY
        
        MOV AX, 0137H           ;显示字符"7 "
        INT 10H                 
        MOV AX, 0120H
        INT 10H
        
        ;CALL DELAY
        ;CALL DELAY
        MOV AL, 20H
        OUT 20H, AL             ;中断结束命令
        IRET
```

## 2. 模板
## 主程序

```ASM
IOY0 EQU 0600H
IOY1 EQU 0640H
IOY2 EQU 0680H
IOY3 EQU 06C0H

A8255 EQU IOY0+00H*2        ;8255的端口A地址
B8255 EQU IOY0+01H*2        ;8255的端口B地址
C8255 EQU IOY0+02H*2        ;8255的端口C地址
CON8255 EQU IOY0+03H*2   ;8255的控制寄存器地址

DATA SEGMENT
DATA ENDS

SSTACK SEGMENT STACK
SSTACK ENDS

CODE SEGMENT
    ASSUME DS:DATA, CS:CODE, SS:SSTACK
START:
    MOV AX, DATA
    MOV DS, AX
    ...
CODE ENDS
END START
```

## 延迟

```ASM
;普通延迟
DELAY:
    PUSH CX
    MOV CX, 0F00H
DELAYLOOP:
    PUSH AX
    POP AX
    LOOP DELAYLOOP
    POP CX
    RET
    
;短延迟
ssDELAY:
    PUSH CX
    MOV CX, 004FH
ssdelayloop:
    PUSH AX
    POP AX
    LOOP ssdelayloop
    POP CX
    RET
    
;长延迟
lldelay:
        PUSH CX
        MOV CX, 4F00H
lldelayloop:    
        PUSH AX
        POP  AX
        LOOP lldelayloop
        POP CX
        RET
```

## 显示六个数码管的值

```ASM
DATA SEGMENT
T1  DB 00H          ;Ti表用于记录6个数码管的段选码
T2  DB 00H
T3  DB 00H
T4  DB 00H
T5  DB 00H
T6  DB 00H
DATA ENDS

CLEAR:
	PUSH BX
	POSH AX
	POSH SI
	POSH CX
	PUSH DX
	;保存用到的寄存器
    LEA BX,T1           ;以T1为首地址，索引T2~T6
    MOV AL,11011111B    ;最右侧的低位数码管
    MOV SI,00H
    MOV CX,06H          ;数码管显示，与实验八相同
    MC2:
        MOV DX,A8255
        OUT DX,AL
        SHR AL,1 ;从右到左显示数码
        OR AL,11000000B
        PUSH AX         ;保存AX的值
            MOV DX,B8255
            MOV AL,[BX+SI]
            OUT DX,AL
        POP AX          ;恢复AX的值
        INC SI          ;地址偏移自增,得到T2
        CALL DELAY
    LOOP MC2
    ;恢复寄存器
    POP DX
    POP CX
    POP SI
    POP AX
    POP BX
    RET
```

在输出区打印值

```ASM
PRINT:
    PUSH AX
    MOV AL,T1
    ADD AL,30H
    MOV AH, 01H
    INT 10H                 ;显示字符T1
    
    MOV AL,T2
    ADD AL,30H
    MOV AH, 01H
    INT 10H                 ;显示字符T2

    MOV AL,T3
    ADD AL,30H
    MOV AH, 01H
    INT 10H                 ;显示字符T3

    MOV AL,T4
    ADD AL,30H
    MOV AH, 01H
    INT 10H                 ;显示字符T4

    MOV AX, 0120H
    INT 10H                 ;显示空格
    
    CALL DELAY
    CALL DELAY
    MOV AL, 20H
    OUT 20H, AL             ;中断结束命令
    POP AX
    RET
```

## 3. 注意点
## 中断
- INT 16
	- AH=1，检查键盘，无按键ZF=1